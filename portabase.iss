; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=PortaBase
AppVerName=PortaBase 2.1
AppPublisher=Jeremy Bowman
AppPublisherURL=http://www.portabase.org
AppSupportURL=http://www.portabase.org
AppUpdatesURL=http://www.portabase.org
AppReadmeFile=README.txt
AppVersion=2.1
AppCopyright=Copyright (C) 2002-2012 Jeremy Bowman
DefaultDirName={pf}\PortaBase
DefaultGroupName=PortaBase
AllowNoIcons=yes
AllowRootDirectory=yes
ChangesAssociations=yes
LicenseFile=COPYING
PrivilegesRequired=admin
SolidCompression=yes
OutputBaseFilename=PortaBase_2.1
OutputDir=build\windows
VersionInfoVersion=2.1.0.0
ShowLanguageDialog=yes
#if Defined SignIt
  SignTool=kSign /d $qPortaBase 2.1 installer$q /du $qhttp://www.portabase.org$q $f
#endif

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"

[Files]
Source: "build\PortaBase.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "build\windows\CHANGES.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "build\windows\COPYING.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "build\windows\README.txt"; DestDir: "{app}"; Flags: ignoreversion isreadme
Source: "build\windows\mingwm10.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "build\windows\libgcc_s_dw2-1.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "build\windows\QtCore4.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "build\windows\QtGui4.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "build\windows\QtXml4.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "build\windows\accessible\qtaccessiblewidgets4.dll"; DestDir: "{app}\accessible"; Flags: ignoreversion
Source: "build\windows\imageformats\qjpeg4.dll"; DestDir: "{app}\imageformats"; Flags: ignoreversion
Source: "resources\help\_build\_static\*"; DestDir: "{app}\help\_static"; Flags: ignoreversion
Source: "resources\help\_build\cs\*"; DestDir: "{app}\help\cs"; Flags: ignoreversion recursesubdirs
Source: "resources\help\_build\en\*"; DestDir: "{app}\help\en"; Flags: ignoreversion recursesubdirs
Source: "resources\help\_build\fr\*"; DestDir: "{app}\help\fr"; Flags: ignoreversion recursesubdirs
Source: "resources\help\_build\ja\*"; DestDir: "{app}\help\ja"; Flags: ignoreversion recursesubdirs
Source: "resources\help\_build\zh_TW\*"; DestDir: "{app}\help\zh_TW"; Flags: ignoreversion recursesubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Languages]
Name: "en"; MessagesFile: "compiler:Default.isl"
Name: "cs"; MessagesFile: "compiler:Languages\Czech.isl"
Name: "de"; MessagesFile: "compiler:Languages\German.isl"
Name: "es"; MessagesFile: "compiler:Languages\Spanish.isl"
Name: "fr"; MessagesFile: "compiler:Languages\French.isl"
Name: "ja"; MessagesFile: "compiler:Languages\Japanese.isl"
Name: "sk"; MessagesFile: "packaging\windows\Slovak-7-5.5.0.isl"
Name: "zh_TW"; MessagesFile: "packaging\windows\ChineseSimp-12-5.5.0.isl"

[Registry]
Root: HKCR; Subkey: ".pob"; ValueType: string; ValueName: ""; ValueData: "PortaBaseFile"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "PortaBaseFile"; ValueType: string; ValueName: ""; ValueData: "PortaBase File"; Flags: uninsdeletekey
Root: HKCR; Subkey: "PortaBaseFile\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\PORTABASE.EXE,0"
Root: HKCR; Subkey: "PortaBaseFile\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\PORTABASE.EXE"" ""%1"""

[Icons]
Name: "{group}\PortaBase"; Filename: "{app}\PortaBase.exe"; Comment: "A personal database application"
Name: "{group}\{cm:ProgramOnTheWeb,PortaBase}"; Filename: "http://www.portabase.org"
Name: "{group}\{cm:UninstallProgram,PortaBase}"; Filename: "{uninstallexe}"
Name: "{userdesktop}\PortaBase"; Filename: "{app}\portabase.exe"; Tasks: desktopicon

[Run]
Filename: "{app}\PortaBase.exe"; Description: "{cm:LaunchProgram,PortaBase}"; Flags: nowait postinstall skipifsilent

